allprojects {
  plugins.withId("com.carrotsearch.gradle.randomizedtesting") {
    randomizedtesting {
      failOnNoTests = true
      testOpts.maxHeap.defaultValue = "100m"
    }

    test {
      useJUnit()
    }
  }

  buildOptions {
    addBooleanOption("tests.slow", "Enable @Slow tests")
    addBooleanOption("tests.veryslow", "Enable @VerySlow tests")
    addBooleanOption("tests.nightly", "Enable @Nightly tests")
  }

  tasks.withType(Test).configureEach { task ->
    [
      "tests.slow",
      "tests.veryslow",
      "tests.nightly"
    ].each { key ->
      def opt = buildOptions {}.getOption(key)
      if (opt.isPresent()) {
        task.systemProperty(opt.name, opt.asStringProvider().get())
      }
    }

    task.jvmArgs("-XX:+HeapDumpOnOutOfMemoryError")
  }
}

