allprojects {
  plugins.withType(JavaPlugin).configureEach {

    base.archivesName = "carrot2" + project.path.replace(':', '-')

    tasks.withType(JavaCompile).configureEach {
      options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-XDsuppressNotes"
      options.encoding = "UTF-8"
    }

    tasks.withType(Jar).configureEach {
      manifest {
        attributes(
            "Implementation-Vendor": "Carrot2 (https://carrot2.org)",
            "Implementation-Title": "Carrot2 ${project.name}",
            "Implementation-Version": "${project.version} (${replaceTokens['product.gitrev']}, ${replaceTokens['product.buildDate']})")
      }
    }

    tasks.withType(Javadoc).configureEach {
      options.encoding = 'UTF-8'
    }

    tasks.matching { it.name == "javadoc" }.configureEach {
      title = "${project.name} ${project.version} API Documentation"

      options.addBooleanOption('html5', true)
      options.addBooleanOption('Xdoclint:all,-missing', true)
      options.noIndex()
    }
  }
}

subprojects {
  plugins.withType(JavaPlugin).configureEach {
    def moduleName = "${project.group}.${project.name.replaceAll('-', '.')}"
    jar {
      manifest {
        attributes("Automatic-Module-Name": moduleName)
      }
    }
  }
}