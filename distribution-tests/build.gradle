plugins {
  id('java-library')
}

configurations {
  distribution
  distributionZip
}

dependencies {
  distribution project(path: ":distribution", configuration: "distDirArtifact")
  distributionZip project(path: ":distribution", configuration: "distZipArtifact")

  testImplementation project(path: ":infra:tests")
  testImplementation libs.commons.compress
  testImplementation libs.carrotsearch.procfork
  testImplementation libs.carrotsearch.console.launcher
}

test {
  dependsOn configurations.distributionZip

  shouldRunAfter ":core:test"

  maxParallelForks = 2

  doFirst {
    def distZip = configurations.distributionZip.singleFile
    systemProperty("distribution.zip", distZip)
    systemProperty("tests.slow", "true")

    logger.lifecycle("Integration tests of: ${distZip}")
  }
}
